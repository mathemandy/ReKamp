buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.37"

        classpath "com.android.tools.build:gradle:$gradle_android_version"
        classpath group: 'net.rubygrapefruit', name: 'native-platform', version: '0.21'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_plugin_version"
    }
}

plugins {
    id("org.jetbrains.kotlin.multiplatform").version("1.3.61")
    id("maven-publish")
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
    mavenLocal()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
}

kotlin {
    jvm()

    android {
        publishLibraryVariants("release", "debug")
    }

    iosArm32() {
        binaries {
            framework()
        }
    }
    iosArm64() {
        binaries {
            framework()
        }
    }
    iosX64() {
        binaries {
            framework()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }

        androidMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }

        iosArm64Main {
            dependsOn(nativeMain)
        }

        iosArm32Main {
            dependsOn(nativeMain)
        }

        iosX64Main {
            dependsOn(nativeMain)
        }
    }
}

afterEvaluate {
    project.publishing.publications.all {
        groupId = group
        if (it.name.contains('metadata')) {
            artifactId = "$libraryName"
        } else {
            artifactId = "$libraryName-$name"
        }
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }.findAll {
        it != "kotlinMultiplatform"
    }
}

bintrayUpload.dependsOn publishToMavenLocal

apply from: rootProject.file('pom.gradle')
apply from: rootProject.file('gradle/bintray.gradle')
apply from: rootProject.file('gradle/publish.gradle')
